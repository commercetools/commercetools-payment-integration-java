// maven publish settings
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    scm {
        connection "scm:git:$scmHttpsUrl"
        developerConnection "scm:git:$scmSshUrl"
        url "$scmProjectUrl"
    }
    developers {
        developer {
            id 'ahalberkamp'
            name 'Andreas Halberkamp'
            url 'https://github.com/ahalberkamp'
            email 'andreas.halberkamp@commercetools.de'
        }
        developer {
            id 'MGA-dotSource'
            name 'Mirco'
            url 'https://github.com/MGA-dotSource'
        }
        developer {
            id 'mht-dotsource'
            name 'Martin Horatschek'
            url 'https://github.com/mht-dotsource'
        }
    }
}

// maven-specific publishing settings
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            groupId rootProject.group
            artifactId project.name
            version rootProject.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'The commercetools java payment project intend is to make payment integration easy')
                root.appendNode('name', project.name)
                root.appendNode('url', scmHttpsUrl)

                root.children().last() + pomConfig
            }
        }
    }
}

signing {
    def signingKey = System.getenv("PGP_KEY")
    def signingPassword = System.getenv("PGP_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
